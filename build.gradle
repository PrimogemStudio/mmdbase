import org.gradle.internal.os.OperatingSystem

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
    id 'application'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.dokka' version '1.9.10'
}

group = 'io.github.primogemstudio'
version = '0.0.3'

project.ext.lwjglVersion = "3.3.3"
switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        def osArch = System.getProperty("os.arch")
        if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
            project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
        }
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.contains("64") ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}" : "natives-windows-x86"
        break
}

repositories {
    maven { url = 'https://raw.githubusercontent.com/kotlin-graphics/mary/master' }
    /*maven { url = 'https://maven.aliyun.com/repository/jcenter' }
    maven { url = 'https://maven.aliyun.com/repository/google' }
    maven { url = 'https://maven.aliyun.com/repository/public' }*/
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    implementation "org.joml:joml:1.10.5"
    implementation "kotlin.graphics:glm:0.9.9.1-11"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.7.3"
    implementation "org.jmonkeyengine:jme3-jbullet:3.6.1-stable"

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-jemalloc"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-jemalloc::$lwjglNatives"
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(17)
}

application {
    mainClassName = 'MainKt'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = "sources"
}

task javadocJar(type: Jar) {
    dependsOn(javadoc)
    from("build/dokka/javadoc")
    archiveClassifier = "javadoc"
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'mmdbase'
            version = version

            from components.kotlin
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'MMD Base'
                description = 'An independent module from Advanced-Framework mod, be used to praise and load MMD models'
                url = 'http://github.com/PrimogemStudio/mmdbase'
                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE Version 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'hackermdch'
                        name = 'hackermdch'
                    }


                    developer {
                        id = 'Jack253-png'
                        name = 'Coder 2'
                        email = 'guoxiuchen20170402@163.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/PrimogemStudio/mmdbase.git'
                    developerConnection = 'scm:git:ssh://github.com/PrimogemStudio/mmdbase.git'
                    url = 'https://github.com/PrimogemStudio/mmdbase'
                }
            }
        }
    }

    repositories {
        maven {
            name = "LocalMaven"
            url = layout.projectDirectory.dir("../maven")
        }

        maven {
            name = "MavenCentral"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUserName
                password = ossrhPassword
            }
            url = snapshotsRepoUrl
        }
    }
}

signing {
    sign publishing.publications.maven
}